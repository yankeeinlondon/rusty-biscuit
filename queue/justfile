set dotenv-load
set positional-arguments

BOLD := '\033[1m'
RESET := '\033[0m'

default:
    @echo "{{BOLD}}Queue CLI and Library{{RESET}}"
    @echo
    @just --list | grep -v 'default'

# build library and CLI
build *args="":
    @echo "Building ..."
    @echo
    @cargo build -p queue-lib {{args}}
    @cargo build -p queue-cli {{args}}
    @echo

# test library and CLI
test *args="":
    @echo "Testing {{BOLD}}Library{{RESET}} code"
    @echo ""
    @cargo test -p queue-lib {{args}}
    @echo ""
    @echo "Testing {{BOLD}}CLI{{RESET}} code"
    @echo ""
    @cargo test -p queue-cli {{args}}

# lint with clippy
lint *args="":
    @cargo clippy -p queue-lib -p queue-cli {{args}}

# install latest release version of CLI
install *args="":
    @cargo install --path cli {{args}}

# build and show the docs for the library code
docs:
    @echo "Building and then showing latest docs for the library code"
    @echo ""
    @cargo doc -p queue-lib --open

# run the debug release of the Queue CLI
cli *args="":
    @cargo run -p queue-cli -- {{args}}
